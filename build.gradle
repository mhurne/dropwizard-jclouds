buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.github.townsfolk:gradle-release:1.2"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5"
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "groovy"
    apply plugin: "maven"
    apply plugin: "maven-publish"

    group = "com.commercehub.dropwizard"
    sourceCompatibility = "1.7"

    repositories {
        jcenter()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }

    dependencies {
        testCompile "org.spockframework:spock-core:1.0-groovy-2.3-SNAPSHOT"
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        from sourceSets.main.allSource
        classifier "sources"
        extension "jar"
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        from javadoc.destinationDir
        classifier "javadoc"
        extension "jar"
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifact sourcesJar {
                    classifier "sources"
                }

                artifact javadocJar {
                    classifier "javadoc"
                }
            }
        }
    }

    apply plugin: "release"
    apply plugin: "com.jfrog.bintray"

    bintray {
        user = project.hasProperty("bintrayUserName") ? bintrayUserName : null
        key = project.hasProperty("bintrayApiKey") ? bintrayApiKey : null
        publications = ["mavenJava"]
        pkg {
            repo = "main"
            userOrg = "commercehub-oss"
            name = "jackson-datatype-mongo"
            desc = "A Jackson module to support JSON serialization and deserialization of MongoDB data types"
            licenses = ['Apache-2.0']
            labels = ["jackson", "mongodb"]
        }
    }

    bintrayUpload.dependsOn build, sourcesJar, javadocJar

    createReleaseTag.dependsOn bintrayUpload
}
